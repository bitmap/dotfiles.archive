#!/bin/zsh

local DOTS="$HOME/.dotfiles"

if [[ "$PWD" != "$HOME/.dotfiles" ]]; then
	print -P "%F{1}Error!%f Run script %F{4}./install%f from %F{3}$DOTS%f"
	exit 1
fi

is_linux () { [[ "$OSTYPE" == *'linux'* ]]; }
is_macOS () { [[ "$OSTYPE" == *'darwin'* ]]; }

function install() {
	local function linked() {
		print -P "%F{6}Linked%f ${1}"
	}

	local function err() {
		print -P "%F{1}Error!%f ${1}"
	}

	local function mkdirs() {
		local dirs=(
			$HOME/.vim/undo
			$HOME/dev
			$HOME/scratch
		)

		for newdir in ${dirs[@]}; do

		if [ ! -d "$newdir" ]; then
			mkdir "$newdir"
		fi
		done
	}

	echo ""

	local files=(
		.zshrc
		.vimrc
		.gitconfig
		.gitignore
		.nvm/default-packages
	)

	local function copy_vscode_settings() {
		local snippets="${1}/snippets"
		local settings="${1}/settings.json"

		# create settings file and snippets dir if they don't exist
		touch "$settings"

		if [ ! -d "$snippets" ]; then
			mkdir "$snippets"
		fi

		#symlink settings
		rm "$settings"
		(ln -s "$DOTS/.config/Code/User/settings.json" "$settings" && linked "VS Code Settings") || err "Error linking VS Code Settings"

		#symlink snippets
		rm -rf "$snippets"
		(ln -s "$DOTS/.config/Code/User/snippets/" "$snippets" && linked "VS Code Snippets") || err "Error linking VS Code Snippets"
	}


	print -P "%F{3}Symlinking%f from %F{4}$DOTS%f"

	# symlink bin
	rm "$HOME/bin"
	ln -s "$DOTS/bin" "$HOME/bin" && linked "~/bin"

	for file in ${files[@]}; do
		local src=$DOTS/$file
		local dest=$HOME/$file

		if [ -f "$dest" ]; then
			# remove old version
			rm -rf "$dest"
		fi

		# make new symlink
		if [ -f "$src" ]; then
			(ln -s "$src" "$dest" && linked "~/$file") || err "Couldn't link %F{5}${file}%f"
		else
			# remove old version
			err "$src doesn't exist"
		fi
	done

	# symlink vscode if it's installed
	local macOS_dir=$HOME/"Library/Application Support/Code/User"
	local linux_dir=$HOME/.config/Code/User

	if is_macOS && [ -d "$macOS_dir" ]; then
		copy_vscode_settings "$macOS_dir"
	elif is_linux && [ -d "$linux_dir" ]; then
		copy_vscode_settings "$linux_dir"
	else
		err "Can't find VS Code settings"
	fi

	touch $HOME/.hushlogin
	mkdirs

	print -P "%F{2}...Done!%f"
}

if [ "$1" = "--force" -o "$1" = "-f" ]; then
	install
else
	read "reply?This will overwrite files in your home directory. Continue? "
	if [[ "$reply" =~ ^[Yy]$ ]]; then
		install
	fi
fi
